$.(
    $comment := '*******************************************************';
    $comment := '*******************************************************';
    $comment := 'TRANSFORM FOR MOODLE 3.x Percipio Course Results Import';
    $comment := 'Using a calculated xAPi Activity ID';
    $comment := 'See https://moodle.org/plugins/tool_uploadexternalcontentresults';
    $comment := 'Author: martinholden-skillsoft';
    $comment := 'Date: 27-JAN-2021';
    $comment := 'Dependency: Uses functions from https://www.npmjs.com/package/jsonata-extended';
    $comment := '*******************************************************';
    $comment := '*******************************************************';

    $comment := '*******************************************************';
    $comment := '*******************************************************';
    $comment := 'START OF TRANSFORM';

    $comment := 'The list of required columns in order as per https://github.com/lushonline/moodle-tool_uploadexternalcontentresults/wiki/Format-of-Import-File';
    $comment := 'as at date of this transform.';
    $required_columns := {
        'COURSE_IDNUMBER': '',
        'USER_USERNAME': '',
        'USER_COMPLETED': '',
        'USER_SCORE': ''
    };

    $transformed_data := (
        $.{
            'COURSE_IDNUMBER': (
                $percipiotype := $lookup([{
                    'video': 'video',
                    'course': 'course',
                    'channel': 'channel',
                    'custom channel':'channel',
                    'journey': 'journey',
                    'book': 'book',
                    'book review': 'book',
                    'audiobook': 'audiobook',
                    'book summary': 'book',
                    'audio summary': 'audiobook',
                    'testprep': 'lc',
                    'practice lab': 'lc',
                    'linked content': 'lc'
                    }
                ], ($lowercase(contentType)));

                $percipiotype := $percipiotype ? $percipiotype : 'UNKNOWN';
                $trim('https://xapi.percipio.com/xapi/' & $percipiotype & '/' & contentUuid)
            ),
            'USER_USERNAME': userId,
            'USER_COMPLETED': (
                $comment := 'Channel and Custom Channel types are always marked completed';
                $channelsalwayscompleted := $contains($lowercase(contentType), 'channel');
                $channelsalwayscompleted ? 1 : status ? status = 'Completed' ? 1 : 0 : ''
            ),
            'USER_SCORE': highScore
          }
    );

    $comment := '*******************************************************';
    $comment := 'Merge the $required_columns object and the $transformed_data';

    $merge([$required_columns, $transformed_data]);
)