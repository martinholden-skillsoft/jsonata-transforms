$.(
    $comment := '*******************************************************';
    $comment := '*******************************************************';
    $comment := 'TRANSFORM FOR A SHAREPOINT LIST';
    $comment := 'Author: martinholden-skillsoft';
    $comment := 'Date: 16-DEC-2020';
    $comment := 'Dependency: Uses functions from https://www.npmjs.com/package/jsonata-extended';
    $comment := '*******************************************************';
    $comment := '*******************************************************';

    $comment := '*******************************************************';
    $comment := '*******************************************************';
    $comment := 'Start of Custom JSONata Functions';

    $comment := '*******************************************************';
    $comment := '* Create a description for the page, each of these';
    $comment := '* end with a CR LF';
    $comment := '*     Comma Delimited list of Authors from by';
    $comment := '*     Copyright Publisher and date from publication.publisher';
    $comment := '*     and publication.copyrightYear';
    $comment := '*     ISBN from publication.isbn';
    $comment := '*     localizedMetadata[0].description';
    $comment := '*     The content type contentType.displayLabel';
    $comment := '* @param {object} $row - The object to process';
    $comment := '* @returns {string} object';
    $comment := '*******************************************************';
    $descriptionobject := function($row){(
        $contenttype := $row.contentType.displayLabel ? $join(['Type:', $row.contentType.displayLabel],' ')  : '';
        $by := $count($row.by) = 0 ? '' : 'By: ' & $join($row.by, ', ') ;
        $publisher := $row.publication ? $row.publication.publisher ? $join(['Copyright:', $row.publication.publisher],' ')  : '' : '';
        $year := $row.publication ? $row.publication.copyrightYear ? $join(['(c)', $string($row.publication.copyrightYear)],' ') : '' : '';
        $copyright := $row.publication ? $join([$publisher, $year],' ')  : '';
        $isbn := $row.publication ? $row.publication.isbn ? $join(['ISBN:', $row.publication.isbn],' ')  : '' : '';
        $description := $row.localizedMetadata[0].description ? $htmltotext($row.localizedMetadata[0].description)  : '';

        $lastupdated := $row.lifecycle.lastUpdatedDate ? $join(['Last Updated:',$moment($row.lifecycle.lastUpdatedDate).fromNow()],' ') : '';

        $parts :=  $filter([$contenttype,$by,$copyright,$isbn,$description,$lastupdated], function($v, $i, $a) {
            $v != ''
            });

        $join($parts,'\r\n')
    )};
    $comment := 'End of Custom JSONata Functions';
    $comment := '*******************************************************';
    $comment := '*******************************************************';


    $comment := '*******************************************************';
    $comment := '*******************************************************';
    $comment := 'START OF TRANSFORM';

    $comment := '*******************************************************';
    $comment := 'Transform the basic data, uses the CONSTANTS specified above';

    $required_columns := {
        'TITLE': '',
        'DESCRIPTION': '',
        'UUID': '',
        'LAUNCH': '',
        'THUMBNAIL' : '',
        'TYPE': '',
        'MINUTES': '',
        'MODALITY': '',
        'STATUS': '',
        'LANGUAGE': ''
    };

    $transformed_data := (
        $.{
            'TITLE': localizedMetadata[0].title,
            'DESCRIPTION': $descriptionobject($),
            'UUID': id,
            'LAUNCH': link,
            'THUMBNAIL': imageUrl,
            'TYPE': contentType ? contentType.displayLabel : '',
            'MINUTES': (
                $duration := duration ? $momentDuration(duration) : $momentDuration('PT0');
                $floor($duration.minutes())
             ),
            'MODALITY': (
                modalities ? $join(modalities,'|') : ''
            ),
            'STATUS': lifecycle.status,
            'LANGUAGE': $languageInfo(localeCodes[0]).language.name
          }
    );

    $comment := '*******************************************************';
    $comment := 'Merge the $required_columns object and the $transformed_data';

    $merge([$required_columns, $transformed_data]);
)